plugins {
    id 'com.google.protobuf' version '0.8.8'
}

repositories {
    maven { // The google mirror is less flaky than mavenCentral()
        url "https://maven-central.storage-download.googleapis.com/repos/central/data/"
    }
    mavenLocal()
}

// IMPORTANT: You probably want the non-SNAPSHOT version of gRPC. Make sure you
// are looking at a tagged version of the example and not "master"!

// Feel free to delete the comment at the next line. It is just for safely
// updating the version in our release process.
def grpcVersion = '1.26.0' // CURRENT_GRPC_VERSION
def protocVersion = '3.11.0'


jar{
    archiveBaseName = "grpc-lib"
    archiveVersion = "0.1.0-SNAPSHOT"
}

dependencies {
    // grpc-alts transitively depends on grpc-netty-shaded, grpc-protobuf, and grpc-stub
    implementation "io.grpc:grpc-alts:${grpcVersion}"
    compileOnly "javax.annotation:javax.annotation-api:1.2"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

// Inform IDEs like IntelliJ IDEA, Eclipse or NetBeans about the generated code.
sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}



buildscript {

    def springBootVersion = '2.3.4.RELEASE'
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath  group: 'org.springframework.boot', name:'spring-boot-gradle-plugin', version: springBootVersion
        classpath  group: 'mysql', name: 'mysql-connector-java', version: '5.1.47'
        classpath  group: 'org.yaml', name: 'snakeyaml', version: '1.19'
    }
}

plugins {
    id "org.flywaydb.flyway" version "5.2.4"
}

/**
 * jacocoのカバレッジレポートを出力
 */
jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
//buildタスクに組み込む
build.dependsOn jacocoTestReport


/**
 * flywayの接続対象
 */
flyway {
    def env = System.getenv("spring.profiles.active") ?: "local"
    def applicationYml = new org.yaml.snakeyaml.Yaml()
            .load(new File(projectDir, "src/main/resources/" + "application-" + env  +".yml")
                    .newInputStream())

    url = "${applicationYml.spring.flywayMigrate.url}"
    user = "${applicationYml.spring.flywayMigrate.username}"
    password = "${applicationYml.spring.flywayMigrate.password}"
}

test {
    useJUnitPlatform()
}

def grpcVersion = '1.26.0'

dependencies {
    //import spring framework
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'
    implementation group: 'org.springframework.boot', name: 'spring-boot-devtools'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-thymeleaf'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-security'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-redis'
    implementation group: 'org.springframework.session', name: 'spring-session-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'io.github.resilience4j:resilience4j-spring-boot2:1.6.1'
    //mysql connector
    implementation group: 'mysql', name: 'mysql-connector-java', version: '5.1.47'
    //logger
    implementation group: 'com.google.flogger', name: 'flogger', version: '0.4'
    runtimeOnly group: 'com.google.flogger', name: 'flogger-system-backend', version: '0.4'
    //apache-commons-lang3
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    //grpc
    implementation group: 'io.github.lognet', name: 'grpc-spring-boot-starter', version: '3.5.1'
    implementation "io.grpc:grpc-alts:${grpcVersion}"
    compileOnly group: 'javax.annotation', name :'javax.annotation-api', version:'1.3.2'

    //submodules
    implementation project(":project-grpc")

    //test tools
    testImplementation group: 'org.dbunit',     name: 'dbunit',             version: '2.5.3'
    testImplementation ('org.springframework.boot:spring-boot-starter-test'){
        exclude module: 'junit'
    }
    testImplementation group: 'org.springframework.security', name: 'spring-security-test'
}

